;;
;; Constructor instantiation
;;

;; Partition constructor parameters into default and non-default parameters
dec $partition_default_parameters(parameterIR*, id*) : (parameterIR*, parameterIR*)

def $partition_default_parameters(eps, id_default*) = (eps, eps)
def $partition_default_parameters(
    parameterIR_h :: parameterIR_t*,
    id_default*
  )
  = (parameterIR_nondefault*, parameterIR_h :: parameterIR_default*)
  -- if parameterIR_h = _ _ _ id_h _
  -- if $partition_default_parameters(parameterIR_t*, id_default*)
      = (parameterIR_nondefault*, parameterIR_default*)
  -- if id_h <- id_default*

def $partition_default_parameters(
    parameterIR_h :: parameterIR_t*,
    id_default*
  )
  = (parameterIR_h :: parameterIR_nondefault*, parameterIR_default*)
  -- if parameterIR_h = _ _ _ id_h _
  -- if $partition_default_parameters(parameterIR_t*, id_default*)
      = (parameterIR_nondefault*, parameterIR_default*)
  -- if ~(id_h <- id_default*)

;; Align constructor parameters with arguments
dec $align_cparams_with_args(constructorParameterIR*, argumentIR*, id*) : (constructorParameterIR*, argumentIR*, constructorParameterIR*, value*)
dec $align_cparams_with_args'(map<id, constructorParameterIR>, constructorParameterIR*, argumentIR*) : (constructorParameterIR*, argumentIR*)

def $align_cparams_with_args(constructorParameterIR*, argumentIR*, id_default*)
  = (constructorParameterIR_aligned*, argumentIR_aligned*, constructorParameterIR_default*, value_default*)
  -- if $partition_default_parameters(constructorParameterIR*, id_default*)
      = (constructorParameterIR_nondefault*, constructorParameterIR_default*)
  -- if (constructorParameterIR_nondefault = _ _ _ id _)*
  -- if $align_cparams_with_args'(`{ (id `: constructorParameterIR_nondefault)* }, constructorParameterIR_nondefault*, argumentIR*)
      = (constructorParameterIR_aligned*, argumentIR_aligned*)
  -- if (constructorParameterIR_default = _ _ _ _ (`= value_default))*

def $align_cparams_with_args'(`{ (id_map `: constructorParameterIR_map)* }, eps, eps) = (eps, eps)
def $align_cparams_with_args'(
  `{ (id_map `: constructorParameterIR_map)* },
  constructorParameterIR_h :: constructorParameterIR_t*,
  argumentIR_h ::argumentIR_t*,
) = (constructorParameterIR_h :: constructorParameterIR_aligned*, argumentIR_h :: argumentIR_aligned*)
  -- if argumentIR_h = typedExpressionIR 
  -- if $align_cparams_with_args'(`{ (id_map `: constructorParameterIR_map)* }, constructorParameterIR_t*, argumentIR_t*)
      = (constructorParameterIR_aligned*, argumentIR_aligned*)
def $align_cparams_with_args'(
  `{ (id_map `: constructorParameterIR_map)* },
  constructorParameterIR_h :: constructorParameterIR_t*,
  argumentIR_h ::argumentIR_t*,
) = (constructorParameterIR_h :: constructorParameterIR_aligned*, argumentIR_h :: argumentIR_aligned*)
  -- if argumentIR_h = `_
  -- if $align_cparams_with_args'(`{ (id_map `:  constructorParameterIR_map)* }, constructorParameterIR_t*, argumentIR_t*)
      = (constructorParameterIR_aligned*, argumentIR_aligned*)
def $align_cparams_with_args'(
  `{ (id_map `: constructorParameterIR_map)* },
  constructorParameterIR_h :: constructorParameterIR_t*,
  argumentIR_h ::argumentIR_t*,
) = (constructorParameterIR_matching :: constructorParameterIR_aligned*, argumentIR_h :: argumentIR_aligned*)
  -- if argumentIR_h = nameIR `= _
  -- if $find_map<id, constructorParameterIR>(`{ (id_map `: constructorParameterIR_map)* }, nameIR)
      = constructorParameterIR_matching
  -- if $align_cparams_with_args'(`{ (id_map `:  constructorParameterIR_map)* }, constructorParameterIR_t*, argumentIR_t*)
      = (constructorParameterIR_aligned*, argumentIR_aligned*)

rule Bind_arg:
  p_caller Ci_caller ; p_callee Ci_callee_0 sto_0 |- id argumentIR ==> Ci_callee_1 sto_1
  -- if $enter_path(Ci_caller, id) = Ci_caller_inner
  -- Argument_inst: p_caller Ci_caller_inner sto_0 |- argumentIR ==> sto_1 value
  -- if $add_value_inst(p_callee, Ci_callee_0, id, value) = Ci_callee_1

rule Bind_args/nil:
  p_caller Ci_caller ; p_callee Ci_callee sto |- eps eps ==> Ci_callee sto

rule Bind_args/cons:
  p_caller Ci_caller ; p_callee Ci_callee_0 sto_0 |- (id_h :: id_t*) (argumentIR_h :: argumentIR_t*)
                                                 ==> Ci_callee_2 sto_2
  -- Bind_arg: p_caller Ci_caller ; p_callee Ci_callee_0 sto_0 |- id_h argumentIR_h ==> Ci_callee_1 sto_1
  -- Bind_args: p_caller Ci_caller ; p_callee Ci_callee_1 sto_1 |- id_t* argumentIR_t* ==> Ci_callee_2 sto_2

;; syntax consDyn

;;; EXTERN nameIR `< typeParameterListIR > `( constructorParameterListIR ) `{ methodPrototypeListIR }

rule Constructor_inst/extern:
  p Ci sto_0 |- (EXTERN nameIR `< typeParameterIR* > `( constructorParameterIR* ) `{ methodPrototypeIR* })
                  `< typeArgumentIR* > `( argumentIR* `# id_default* )
            ==> sto_1 objDyn
  ---- ;; initialize callee context
  -- if BLOCK = p_callee
  -- if $copy_context_inst(GLOBAL, Ci) = Ci_callee_0
  ---- ;; bind type arguments to the callee context
  -- if $add_types_inst(p_callee, Ci_callee_0, typeParameterIR*, typeArgumentIR*) = Ci_callee_1
  -- if $align_cparams_with_args(constructorParameterIR*, argumentIR*, id_default*)
      = (constructorParameterIR_aligned*, argumentIR_aligned*, constructorParameterIR_default*, value_default*)
  ---- ;; bind non-default arguments to callee context
  -- if (constructorParameterIR_aligned = _ _ _ id_aligned _)*
  -- Bind_args: p Ci ; p_callee Ci_callee_1 sto_0 |- id_aligned* argumentIR_aligned* ==> Ci_callee_2 sto_1
  ---- ;; bind default values to callee context
  -- if (constructorParameterIR_default = _ _ _ id_cparam_default _)*
  -- if $add_values_inst(p_callee, Ci_callee_2, id_cparam_default*, value_default*) = Ci_callee_3
  ---- ;; instantiate method prototypes
  -- ExternMethods_inst: Ci_callee_3 |- methodPrototypeIR* ==> Ci_callee_4
  ---- ;; create extern object with final callee context
  -- if EXTERN `{ nameIR Ci_callee_4.BLOCK.THETA Ci_callee_4.BLOCK.VENV Ci_callee_4.BLOCK.RENV }
      = objDyn

;;; PARSER `< typeParameterListIR > `( parameterListIR ) `( constructorParameterListIR ) `{ parserLocalDeclarationListIR parserStateListIR }

rule Constructor_inst/parser:
  p Ci sto_0 |- (PARSER `< typeParameterIR* > `( parameterIR* )`( constructorParameterIR* )
                  `{ parserLocalDeclarationIR* parserStateIR* })
                  `< typeArgumentIR* > `( argumentIR* `# id_default* )
            ==> sto_3 objDyn
  ---- ;; initialize callee context
  -- if BLOCK = p_callee
  -- if $copy_context_inst(GLOBAL, Ci) = Ci_callee_0
  ---- ;; bind type arguments to the callee context
  -- if $add_types_inst(p_callee, Ci_callee_0, typeParameterIR*, typeArgumentIR*) = Ci_callee_1
  -- if $align_cparams_with_args(constructorParameterIR*, argumentIR*, id_default*)
      = (constructorParameterIR_aligned*, argumentIR_aligned*, constructorParameterIR_default*, value_default*)
  ---- ;; bind non-default arguments to callee context
  -- if (constructorParameterIR_aligned = _ _ _ id_aligned _)*
  -- Bind_args: p Ci ; p_callee Ci_callee_1 sto_0 |- id_aligned* argumentIR_aligned* ==> Ci_callee_2 sto_1
  ---- ;; bind default values to callee context
  -- if (constructorParameterIR_default = _ _ _ id_cparam_default _)*
  -- if $add_values_inst(p_callee, Ci_callee_2, id_cparam_default*, value_default*) = Ci_callee_3
  -- ParserLocalDecls_inst: Ci_callee_3 sto_1 |- parserLocalDeclarationIR* ==> Ci_local sto_2 parserLocalDeclarationIR_inst*
  ---- ;; add accept and reject states
  -- if stateDyn_empty = `EMPTY `{ eps }
  -- if $add_state_inst(BLOCK, Ci_local, "accept", stateDyn_empty) = Ci_local_1
  -- if $add_state_inst(BLOCK, Ci_local_1, "reject", stateDyn_empty) = Ci_local_2
  ---- ;; instantiate parser states
  -- ParserStates_inst: Ci_local_2 sto_2 |- parserStateIR* ==> Ci_state sto_3
  ---- ;; create parser object with callee value environment and instantiated state environment
  -- if PARSER `{ Ci_callee_3.BLOCK.VENV parameterIR* parserLocalDeclarationIR_inst* Ci_state.BLOCK.SENV }
      = objDyn

;;; CONTROL `< typeParameterListIR > `( parameterListIR ) `( constructorParameterListIR ) `{ controlLocalDeclarationListIR APPLY controlBodyIR }

rule Constructor_inst/control:
  p Ci sto_0 |- (CONTROL `< typeParameterIR* > `( parameterIR* ) `( constructorParameterIR* )
                  `{ controlLocalDeclarationIR* APPLY controlBodyIR })
                  `< typeArgumentIR* > `( argumentIR* `# id_default* )
            ==> sto_3 objDyn
  ---- ;; initialize callee context
  -- if BLOCK = p_callee
  -- if $copy_context_inst(GLOBAL, Ci) = Ci_callee_0
  ---- ;; bind type arguments to the callee context
  -- if $add_types_inst(p_callee, Ci_callee_0, typeParameterIR*, typeArgumentIR*) = Ci_callee_1
  -- if $align_cparams_with_args(constructorParameterIR*, argumentIR*, id_default*)
      = (constructorParameterIR_aligned*, argumentIR_aligned*, constructorParameterIR_default*, value_default*)
  ---- ;; bind non-default arguments to callee context
  -- if (constructorParameterIR_aligned = _ _ _ id_aligned _)*
  -- Bind_args: p Ci ; p_callee Ci_callee_1 sto_0 |- id_aligned* argumentIR_aligned* ==> Ci_callee_2 sto_1
  ---- ;; bind default values to callee context
  -- if (constructorParameterIR_default = _ _ _ id_cparam_default _)*
  -- if $add_values_inst(p_callee, Ci_callee_2, id_cparam_default*, value_default*) = Ci_callee_3
  ---- ;; instantiate local declarations
  -- ControlLocalDecls_inst: Ci_callee_3 sto_1 |- controlLocalDeclarationIR* ==> Ci_local sto_2 controlLocalDeclarationIR'*
  ---- ;; instantiate control body
  -- Block_inst: LOCAL Ci_local sto_2 |- controlBodyIR ==> _ sto_3 controlBodyIR'
  ---- ;; create control object with callee value environment and local routine environment
  -- if CONTROL `{ Ci_callee_3.BLOCK.VENV parameterIR* controlLocalDeclarationIR'*
          Ci_local.BLOCK.RENV controlBodyIR' }
      = objDyn

;;; PACKAGE `< typeParameterListIR > `( constructorParameterListIR )

rule Constructor_inst/package:
  p Ci sto_0 |- (PACKAGE `< typeParameterIR* > `( constructorParameterIR*))
                  `< typeArgumentIR* > `( argumentIR* `# id_default* )
            ==> sto_1 objDyn
  ---- ;; initialize callee context
  -- if BLOCK = p_callee
  -- if $copy_context_inst(GLOBAL, Ci) = Ci_callee_0
  ---- ;; bind type arguments to the callee context
  -- if $add_types_inst(p_callee, Ci_callee_0, typeParameterIR*, typeArgumentIR*) = Ci_callee_1
  -- if $align_cparams_with_args(constructorParameterIR*, argumentIR*, id_default*)
      = (constructorParameterIR_aligned*, argumentIR_aligned*, constructorParameterIR_default*, value_default*)
  ---- ;; bind non-default arguments to callee context
  -- if (constructorParameterIR_aligned = _ _ _ id_aligned _)*
  -- Bind_args: p Ci ; p_callee Ci_callee_1 sto_0 |- id_aligned* argumentIR_aligned* ==> Ci_callee_2 sto_1
  ---- ;; bind default values to callee context
  -- if (constructorParameterIR_default = _ _ _ id_cparam_default _)*
  -- if $add_values_inst(p_callee, Ci_callee_2, id_cparam_default*, value_default*) = Ci_callee_3
  ---- ;; create package object with final callee context
  -- if PACKAGE `{ Ci_callee_3.BLOCK.THETA Ci_callee_3.BLOCK.VENV }
      = objDyn

;;; TABLE nameIR `{ tablePropertyListIR }

dec $init_table(tablePropertyListIR) : tablePropertyListIR

;; (TODO) change output type and add instantiation logic for dynamic semantics
def $init_table(tablePropertyListIR) = tablePropertyListIR

rule Constructor_inst/table:
  p Ci sto |- (TABLE nameIR `{ tablePropertyListIR })
              `< typeArgumentIR* > `( argumentIR* `# id_default* )
          ==> sto_2 objDyn
  ---- ;; initialize callee context
  -- if LOCAL = p_callee
  -- if $copy_context_inst(BLOCK, Ci) = Ci_callee_0
  ---- ;; bind type arguments to the callee context
  -- if $add_types_inst(p_callee, Ci_callee_0, eps, typeArgumentIR*) = Ci_callee_1
  -- if $align_cparams_with_args(eps, argumentIR*, id_default*)
      = (constructorParameterIR_aligned*, argumentIR_aligned*, constructorParameterIR_default*, value_default*)
  ---- ;; bind non-default arguments to callee context
  -- if (constructorParameterIR_aligned = _ _ _ id_aligned _)*
  -- Bind_args: p Ci ; p_callee Ci_callee_1 sto |- id_aligned* argumentIR_aligned* ==> Ci_callee_2 sto_1
  ---- ;; bind default values to callee context
  -- if (constructorParameterIR_default = _ _ _ id_cparam_default _)*
  -- if $add_values_inst(p_callee, Ci_callee_2, id_cparam_default*, value_default*) = Ci_callee_3
  ---- ;; create table frame
  -- if $enter_inst(Ci_callee_3) = Ci_frame
  ---- ;; insantiate table properties
  -- TableProperties_inst: Ci_frame sto_1 |- tablePropertyListIR ==> Ci_table sto_2 tablePropertyListIR_inst
  -- if $init_table(tablePropertyListIR_inst) = tablePropertyListIR_dynamic
  ---- ;; create table object with table frame
  -- if Ci_table.LOCAL.VENVS = venv :: _
  -- if TABLE `{ nameIR venv tablePropertyListIR_dynamic }
      = objDyn
